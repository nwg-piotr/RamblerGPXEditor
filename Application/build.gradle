buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
    }
}

apply plugin: 'com.android.application'

dependencies {
    compile files('libs/httpclient-4.5.jar')
    compile files('libs/httpcore-4.4.1.jar')
    compile files('libs/httpmime-4.5.jar')
}

repositories {
    jcenter()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile 'com.google.android.gms:play-services-location:11.0.0'
    compile 'com.android.support:support-v13:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'org.osmdroid:osmdroid-android:5.6.4'
    compile 'com.github.MKergall:osmbonuspack:6.3'
    compile project(':Karambola')
}

// The sample build uses multiple directories to
// keep boilerplate and common code separate from
// the main sample code.
List<String> dirs = [
        'main',     // main sample code; look here for the interesting stuff.
        'common',   // components that are reused by multiple samples
        'template'] // boilerplate code that is generated by the sample template process

android {

    compileSdkVersion 25
    buildToolsVersion '25.0.3'
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }
    defaultConfig {
        applicationId "pl.nwg.dev.rambler.gpx"
        minSdkVersion 18
        targetSdkVersion 25
        versionCode 39
        versionName "0.0.3.9"
        setProperty("archivesBaseName", "RamblerGPXeditor")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            dirs.each { dir ->
                java.srcDirs "src/${dir}/java"
                res.srcDirs "src/${dir}/res"
            }
        }
        androidTest.setRoot('tests')
        androidTest.java.srcDirs = ['tests/src']

    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    output.outputFile.name.replace(".apk", "-${variant.versionName}.apk"))
        }
    }

    useLibrary 'org.apache.http.legacy'
}















